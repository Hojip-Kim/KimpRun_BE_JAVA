plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.0'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'org.flywaydb.flyway' version '10.10.0'
}

group = 'kimp'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

ext {
    querydslVersion = "5.0.0"
}

// QueryDSL 생성 디렉토리를 먼저 정의
def querydslDir = layout.buildDirectory.dir('generated/querydsl').get().asFile

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', querydslDir]
        }
    }

    integrationTest{
        java.srcDir file('src/test/java/integration')
        resources.srcDir file('src/test/resources')
        compileClasspath += sourceSets.main.output + configurations.testCompileClasspath
        runtimeClasspath += output + compileClasspath + configurations.testRuntimeClasspath
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

task integrationTest(type: Test) {
    description = '통합 테스트 실행'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    useJUnitPlatform()

    outputs.upToDateWhen { false }
}

test {
    useJUnitPlatform()
    exclude '**/*IntegrationTest.*'
    exclude '**/*IT.*'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.java-websocket:Java-WebSocket:1.5.2'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework.security:spring-security-messaging'
    implementation 'org.postgresql:postgresql:42.7.3'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    
    implementation 'org.redisson:redisson-spring-boot-starter:3.39.0'
    implementation 'io.github.bonigarcia:webdrivermanager:5.5.0'
    implementation 'org.jsoup:jsoup:1.18.3'
    implementation 'com.slack.api:slack-api-client:1.43.0'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client:2.6.2'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'net.logstash.logback:logstash-logback-encoder:7.3'

    // Spring Batch
    implementation 'org.springframework.boot:spring-boot-starter-batch'
    
    // Flyway
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-database-postgresql'
    
    // MyBatis
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.3'

    // Lombok - QueryDSL보다 먼저 처리되어야 함
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // QueryDSL
    implementation "com.querydsl:querydsl-jpa:${querydslVersion}:jakarta"
    annotationProcessor "com.querydsl:querydsl-apt:${querydslVersion}:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.postgresql:postgresql'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'com.h2database:h2'

    // 테스트 소스셋에도 annotation processor 적용
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testAnnotationProcessor "com.querydsl:querydsl-apt:${querydslVersion}:jakarta"
}

// QueryDSL 생성 디렉토리 정리 태스크
tasks.register('cleanQuerydsl', Delete) {
    delete querydslDir
}

// clean 태스크가 실행될 때 QueryDSL 디렉토리도 정리
tasks.named('clean') {
    dependsOn 'cleanQuerydsl'
}

tasks.withType(JavaCompile).configureEach {
    options.generatedSourceOutputDirectory = querydslDir
    options.encoding = "UTF-8"
    // options.compilerArgs += ["-Xlint:deprecation", "-Xlint:unchecked"]
}

// compileJava 태스크가 실행되기 전에 QueryDSL 생성 디렉토리가 존재하도록 보장
tasks.named('compileJava') {
    doFirst {
        querydslDir.mkdirs()
    }
}

tasks.named('test') {
    useJUnitPlatform()
    systemProperty 'file.encoding', 'UTF-8'
}

// Flyway 설정
flyway {
    url = 'jdbc:postgresql://localhost:5432/kimprun'
    user = 'hojipkim'
    password = 'Zkdltl12!@'
    locations = ['classpath:db/migration']
    baselineOnMigrate = true
    validateOnMigrate = true
}
